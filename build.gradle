plugins {
    id "me.champeau.gradle.jmh" version "0.2.0"
    id "com.github.hierynomus.license-base" version "0.15.0"
    id 'io.codearte.nexus-staging' version '0.21.1'
}
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'com.github.hierynomus.license-base'
//apply plugin: 'com.github.kt3k.coveralls'

defaultTasks 'clean', 'build', 'shadowJar', 'install'

def mockitoVersion = '3.2.4'
def junitVersion = '5.5.2'
def jmhVersion = '1.22'
def agronaVersion = '1.1.0'

def f4jGroup = 'org.fix4j'
def f4jName = 'fix4j-sbe'
def f4jVersion = file('version.txt').text.trim()

def generatedSrc = "$buildDir/generated-src"
def validationXsdPath = projectDir.toString() + '/src/main/resources/sbe.xsd'

group = f4jGroup
version = f4jVersion
sourceCompatibility = 1.8

sourceSets.main.java {
    srcDirs += generatedSrc
}
sourceSets.main.resources {
//    srcDirs += generatedSrc
    exclude '**/*.template'
    exclude '**/codegen/**'
}

repositories {
    mavenCentral()
}

configurations {
    sbeTool
}

dependencies {
    compile 'org.agrona:agrona:1.0.8'
    sbeTool 'uk.co.real-logic:sbe-tool:1.13.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hdrhistogram:HdrHistogram:2.1.9'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'build/repositories'
       }
    }
}

task generateCodecs(type: JavaExec) {
    main = 'uk.co.real_logic.sbe.SbeTool'
    classpath = configurations.sbeTool
    systemProperties(
            'sbe.output.dir': 'build/generated-src',
            'sbe.target.language': 'Java',
            'sbe.java.generate.interfaces': 'true',
            'sbe.decode.unknown.enum.values': 'true',
            'sbe.validation.stop.on.error': 'true',
            'sbe.xinclude.aware': 'true',
            'sbe.validation.xsd': validationXsdPath)
    args = ['src/test/resources/example-schema.xml', 'src/test/resources/trading-schema.xml']
}

task copyLicense(type: Copy) {
    from('.')
    into('build/resources/main/')
    include('LICENSE.md')
    rename('LICENSE.md', 'LICENSE.txt')
}
task copyLicenseToSrc(type: Copy) {
    from('build/resources/main/')
    into('build/generated-src/')
    include('LICENSE.txt')
}

license {
    header rootProject.file('src/main/resources/LICENSE.template')
    strictCheck true
    include "**/*.java"
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
}

task licenseFormatGen(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
    source = generatedSrc
}

licenseFormatGen.dependsOn generateCodecs
copyLicenseToSrc.dependsOn copyLicense
compileJava.dependsOn generateCodecs,licenseFormatGen
processResources.dependsOn copyLicense,copyLicenseToSrc

javadoc {
	options.showFromPackage()
	options.linkSource()
	options.links("http://docs.oracle.com/javase/8/docs/api/")
	options.windowTitle("${f4jName} API ${version}")
           .docTitle("${f4jName} API ${version}")
	options.overview = "src/main/resources/overview.html"
}

task javadocJar(type: Jar) {
//    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
//    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

//snapshot first, then release:
//gradle clean uploadArchives -Pupload -PossrhUsername=xxx -PossrhPassword=xxx
if (project.hasProperty('upload')) {

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'
        apply plugin: 'io.codearte.nexus-staging'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment {
                        MavenDeployment deployment -> signing.signPom(deployment)
                    }
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name "${f4jName}"
                        packaging 'jar'
                        // optionally artifactId can be defined here
                        description 'A user friendly implementation of the simple binary encoding standard.'
                        url 'https://github.com/fix4j/fix4j-sbe'

                        scm {
                            connection 'scm:git:https://github.com/fix4j/fix4j-sbe.git'
                            developerConnection 'scm:git:https://github.com/fix4j/fix4j-sbe.git'
                            url 'https://github.com/fix4j/fix4j-sbe.git'
                        }
                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://opensource.org/licenses/MIT'
                            }
                        }

                        developers {
                            developer {
                                id 'terzerm'
                                name 'Marco Terzer'
                            }
                            developer {
                                id 'anton-anufriev'
                                name 'Anton Anufriev'
                            }
                        }
                    }
                }
            }
        }

        nexusStaging {
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}
